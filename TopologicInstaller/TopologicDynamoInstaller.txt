;NSIS Modern User Interface
;Basic Example Script
;Written by Joost Verburg

;--------------------------------
;Include Modern UI

  !include "MUI2.nsh"
  !include "Sections.nsh"
  !include "LogicLib.nsh"
  !include "NSISList.nsh"
  !include "WinMessages.nsh"
  ReserveFile "${NSISDIR}\Plugins\NSISList.dll"
 

;--------------------------------
;General
  !define /date MyTIMESTAMP "%Y-%m-%d-%H-%M-%S"

  ;Name and file
  Name "Topologic"
  OutFile "TopologicDynamoInstaller.exe"

  ;Default installation folder
  InstallDir "$APPDATA\Dynamo\Dynamo Core"
  
  ;Request application privileges for Windows Vista
  RequestExecutionLevel admin

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

; https://nsis-dev.github.io/NSIS-Forums/html/t-321778.html
!define __NSD_ListBoxMultiselect_CLASS LISTBOX
!define __NSD_ListBoxMultiselect_STYLE ${DEFAULT_STYLES}|${WS_TABSTOP}|${WS_VSCROLL}|${LBS_DISABLENOSCROLL}|${LBS_HASSTRINGS}|${LBS_NOINTEGRALHEIGHT}|${LBS_NOTIFY}|${LBS_MULTIPLESEL}
!define __NSD_ListBoxMultiselect_EXSTYLE ${WS_EX_WINDOWEDGE}|${WS_EX_CLIENTEDGE}
!insertmacro __NSD_DefineControl ListBoxMultiselect 

; https://nsis.sourceforge.io/Validation_Function
!define Validate "!insertmacro Validate"
 
!macro Validate ResultVar String CharacterSet
	Push "${String}"
	Push "${CharacterSet}"
	Call ValidateInternal
	Pop "${ResultVar}"
!macroend

; https://nsis.sourceforge.io/ShellExecWait 
!macro ShellExecWait verb app param workdir show exitoutvar ;only app and show must be != "", every thing else is optional
#define SEE_MASK_NOCLOSEPROCESS 0x40 
System::Store S
System::Call '*(&i60)i.r0'
System::Call '*$0(i 60,i 0x40,i $hwndparent,t "${verb}",t $\'${app}$\',t $\'${param}$\',t "${workdir}",i ${show})i.r0'
System::Call 'shell32::ShellExecuteEx(ir0)i.r1 ?e'
${If} $1 <> 0
	System::Call '*$0(is,i,i,i,i,i,i,i,i,i,i,i,i,i,i.r1)' ;stack value not really used, just a fancy pop ;)
	System::Call 'kernel32::WaitForSingleObject(ir1,i-1)'
	System::Call 'kernel32::GetExitCodeProcess(ir1,*i.s)'
	System::Call 'kernel32::CloseHandle(ir1)'
${EndIf}
System::Free $0
!if "${exitoutvar}" == ""
	pop $0
!endif
System::Store L
!if "${exitoutvar}" != ""
	pop ${exitoutvar}
!endif
!macroend
 
 
!define ALPHA "abcdefghijklmnopqrstuvwxyz "
!define NUMERIC "1234567890"
!define SPECIAL "~!@#$%^&*()_+|`\=-}{$\":?><][';/.," # workaround for syntax highlighting - '
 
;Push "value to check"
;Push "comparisonlist"
Function ValidateInternal 
  Push $0
  Push $1
  Push $2
  Push $3 ;value length
  Push $4 ;count 1
  Push $5 ;tmp var 1
  Push $6 ;list length
  Push $7 ;count 2
  Push $8 ;tmp var 2
  Exch 9
  Pop $1 ;list
  Exch 9
  Pop $2 ;value
  StrCpy $0 1
  StrLen $3 $2
  StrLen $6 $1
  StrCpy $4 0
  lbl_loop:
    StrCpy $5 $2 1 $4
    StrCpy $7 0
    lbl_loop2:
      StrCpy $8 $1 1 $7
      StrCmp $5 $8 lbl_loop_next 0
      IntOp $7 $7 + 1
      IntCmp $7 $6 lbl_loop2 lbl_loop2 lbl_error
  lbl_loop_next:
  IntOp $4 $4 + 1
  IntCmp $4 $3 lbl_loop lbl_loop lbl_done
  lbl_error:
  StrCpy $0 0
  lbl_done:
  Pop $6
  Pop $5
  Pop $4
  Pop $3
  Pop $2
  Pop $1
  Exch 2
  Pop $7
  Pop $8
  Exch $0
FunctionEnd
;--------------------------------
;Pages
  ;!define MUI_WELCOMEPAGE_TITLE "Test"
  !define MUI_WELCOMEPAGE_TEXT "This Setup will guide you through the installation of Topologic as a Dynamo package.$\r$\n$\r$\nNOTE: Please close all Dynamo instances before proceeding with the next installation steps.$\r$\n$\r$\nClick Next to continue."
  
  !define MUI_ICON "Topologic-Logo-ColourOnWhite.ico"
  !define MUI_HEADERIMAGE
  !define MUI_HEADERIMAGE_BITMAP "TopoLogic-Logo.bmp"
  !define MUI_HEADERIMAGE_UNBITMAP "TopoLogic-Logo.bmp"
  !define MUI_HEADERIMAGE_RIGHT
  
  !insertmacro MUI_PAGE_WELCOME
  !insertmacro MUI_PAGE_LICENSE "TopologicLicense.rtf"
  !insertmacro MUI_PAGE_COMPONENTS
  ;!insertmacro MUI_PAGE_DIRECTORY
  Page custom FunctionInstallVersionEnter FunctionInstallVersionLeave ;Custom page
  !insertmacro MUI_PAGE_INSTFILES
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Installer Sections
LangString PAGE_TITLE ${LANG_ENGLISH} "Choose Install Location"
LangString PAGE_SUBTITLE ${LANG_ENGLISH} "Choose the Dynamo version to install Topologic."
Var Dialog
Var Label
Var ListBoxHWND ;the displayed list box shows the Dynamo versions
Var HiddenPathListBoxHWND ;stores the path, hidden
Var path

Function FunctionInstallVersionEnter
  !insertmacro MUI_HEADER_TEXT $(PAGE_TITLE) $(PAGE_SUBTITLE)
  nsDialogs::Create 1018
  Pop $Dialog

  ${NSD_CreateLabel} 0 0 100% 18% "The installer found the following versions of Dynamo installed on your system. Please select the version for which to install Topologic. You can install for additional versions by re-running this installer."
  Pop $Label
  
  ${NSD_CreateListBox} 0 19% 100% 81% "Dynamo versions"
  Pop $ListBoxHWND
  
  ${NSD_CreateListBox} 0 0 0 0 "Hidden Dynamo paths"
  Pop $HiddenPathListBoxHWND
  ShowWindow $HiddenPathListBoxHWND ${SW_HIDE} # start out hidden

  ${If} $ListBoxHWND != error
    ${If} ${FileExists} "$APPDATA\Dynamo\Dynamo Core\*.*"
		FindFirst $0 $1 "$APPDATA\Dynamo\Dynamo Core\*.*"
		loopCore:
		StrCmp $1 "" doneCore
		StrCmp $1 "." nextCore
		StrCmp $1 ".." nextCore
		
		; get the first letter. only consider number
		StrCpy $2 $1 1 0
		${Validate} $3 $2 ${NUMERIC}
		StrCmp $3 "0" nextCore
		
		${If} ${FileExists} "$APPDATA\Dynamo\Dynamo Core\$1\*.*"
			${NSD_LB_AppendString} $ListBoxHWND "Dynamo Core $1"
			${NSD_LB_AppendString} $HiddenPathListBoxHWND "$APPDATA\Dynamo\Dynamo Core\$1\"
		${EndIf}
		nextCore:
		FindNext $0 $1
		Goto loopCore
		doneCore:
		FindClose $0
	${EndIf}
	
    ${If} ${FileExists} "$APPDATA\Dynamo\Dynamo Revit\*.*"
		FindFirst $0 $1 "$APPDATA\Dynamo\Dynamo Revit\*.*"
		loopRevit:
		StrCmp $1 "" doneRevit
		StrCmp $1 "." nextRevit
		StrCmp $1 ".." nextRevit
		
		; get the first letter. only consider number
		StrCpy $2 $1 1 0
		${Validate} $3 $2 ${NUMERIC}
		StrCmp $3 "0" nextRevit

		${If} ${FileExists} "$APPDATA\Dynamo\Dynamo Revit\$1\*.*"
			${NSD_LB_AppendString} $ListBoxHWND "Dynamo Revit $1"
			${NSD_LB_AppendString} $HiddenPathListBoxHWND "$APPDATA\Dynamo\Dynamo Revit\$1\"
		${EndIf}
		nextRevit:
		FindNext $0 $1
		Goto loopRevit
		doneRevit:
		FindClose $0
	${EndIf}
	
	${NSD_LB_GetCount} $ListBoxHWND $0
	${If} $0 > 0
		System::Call user32::SendMessage(i$ListBoxHWND,i${LB_SETCURSEL},0,0)
		System::Call user32::SendMessage(i$HiddenPathListBoxHWND,i${LB_SETCURSEL},0,0)
	${Else}
		MessageBox MB_OK "No Dynamo packages folder is found. Aborting."
		Abort
	${EndIf}

  ${Else}
    MessageBox MB_OK "Error showing Dynamo version options."  
  ${EndIf}
  
  nsDialogs::Show
FunctionEnd

Var LB_output
Function FunctionInstallVersionLeave
  ${NSD_LB_GetSelectionIndex} $ListBoxHWND $LB_output
  System::Call user32::SendMessage(i$HiddenPathListBoxHWND,i${LB_GETTEXT},i$LB_output,t.r0)
  StrCpy $path $0 ;this stores the path
  StrCpy $OUTDIR $path
  StrCpy $path $OUTDIR
  
FunctionEnd


Section "TopologicDynamo" SectionTopologicDynamo
	SetOutPath "$path\packages\Topologic"
	File pkg.json
	CreateDirectory "$path\packages\Topologic\bin"
	CreateDirectory "$path\packages\Topologic\dyf"
	CreateDirectory "$path\packages\Topologic\extra"
	SetOutPath "$path\packages\Topologic\bin"
	File ..\output\x64\Release\TopologicCore.dll
    File ..\output\x64\Release\TK*.dll
	File ..\output\x64\Release\TopologicDynamo.dll
    File ..\output\x64\Release\TopologicDynamo.customization.dll
    File ..\output\x64\Release\TopologicDynamo.xml
    File ..\output\x64\Release\TopologicDynamo_DynamoCustomization.xml

	Call CheckVCRedistributable
    WriteUninstaller "$path\packages\Topologic\Uninstall.exe"
SectionEnd

Section "TopologicEnergy" SectionTopologicEnergy	
	File ..\output\x64\Release\TopologicEnergy.dll
	File ..\output\x64\Release\TopologicEnergy.customization.dll
	File ..\output\x64\Release\TopologicEnergy.xml
	File ..\output\x64\Release\TopologicEnergy_DynamoCustomization.xml
	File ..\output\x64\Release\OpenStudio.dll
	File ..\output\x64\Release\openstudio_csharp.dll
	File ..\output\x64\Release\openstudio_model_csharp.dll
	File ..\output\x64\Release\openstudio_translators_csharp.dll
SectionEnd
	
Function .onInit
  IntOp $R0 ${SF_RO} | ${SF_SELECTED}
  SectionSetFlags ${SectionTopologicDynamo} $R0
  SectionSetFlags ${SectionTopologicEnergy} 0
  
FunctionEnd

Function CheckVCRedistributable
  ClearErrors
  SetRegView 64
  ReadRegDWORD $0 HKLM "SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\x64" "Installed"
  ${If} ${Errors}
    ;Install Redistributable
	Call InstallVCRedistributable
  ${Else}
    ${If} $0 == ""
      ;Install Redistributable, although this should not happen.
	  Call InstallVCRedistributable
    ${Else} ; Compare version
	  ReadRegDWORD $1 HKLM "SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\x64" "Major"
	  ${If} $1 < 14
	    ;Install Redistributable
	    Call InstallVCRedistributable
	  ${Else}
	    ReadRegDWORD $2 HKLM "SOFTWARE\WOW6432Node\Microsoft\VisualStudio\14.0\VC\Runtimes\x64" "Minor"
		${If} $2 < 14
	      ;Install Redistributable
	      Call InstallVCRedistributable
	    ${EndIf}
	  ${EndIf}
    ${EndIf}
  ${EndIf}
FunctionEnd

Function InstallVCRedistributable
  MessageBox MB_YESNO "No Visual Studio 2017 Redistributable (x64) installation is found. Press Yes to install it, otherwise press No to skip this process (you will need to download and install it separately)." IDYES InstallVCRedistributableYes IDNO InstallVCRedistributableFinish
  InstallVCRedistributableYes:
	File vc_redist.x64.exe
    !insertmacro ShellExecWait "" '"$path\packages\Topologic\bin\vc_redist.x64.exe"' '""' "" ${SW_SHOW} $9
	${If} $9 <> 0
      MessageBox MB_OK "Visual Studio 2017 Redistributable (x64) was not installed."
    ${EndIf}
  InstallVCRedistributableFinish:
  Delete "$path\packages\Topologic\bin\vc_redist.x64.exe"
FunctionEnd
;--------------------------------
;Descriptions


;--------------------------------
;Uninstaller Section

Section "Uninstall"

  ;ADD YOUR OWN FILES HERE...
  Delete "$INSTDIR\*"
  Delete "$INSTDIR\Uninstall.exe"
  RMDir "$INSTDIR"

SectionEnd